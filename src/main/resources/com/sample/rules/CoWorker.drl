package com.sample.rules

rule "Ask if boss"
    salience -10
    when
        // Context: The user has specified the relation is "Co-worker".
        Answer(questionId == "relationMale", value == "Co-worker")
        not Question(id == "isBoss")
        not Answer(questionId == "isBoss")
    then
        Question q = new Question();
        q.setId("isBoss");
        q.setText("Is this person your boss?");
        q.setOptions("No|Yes");
        q.setAnswered(false);
        q.setMultiSelect(false);
        insert(q);
end

rule "Co-worker is not boss"
    when
        Answer(questionId == "relationMale", value == "Co-worker")
        Answer(questionId == "isBoss", value == "No")
    then
        insert(new Recommendation("Holiday 4-Pack (for yourself)", "No need to buy this person a gift. Take the money you would have spent and put it towards a Holiday 4-Pack of your own."));
end

rule "Ask if like boss"
    salience -10
    when
        Answer(questionId == "relationMale", value == "Co-worker")
        Answer(questionId == "isBoss", value == "Yes")
        not Question(id == "likeBoss")
        not Answer(questionId == "likeBoss")
    then
        Question q = new Question();
        q.setId("likeBoss");
        q.setText("Do you like him?");
        q.setOptions("No|Yes");
        q.setAnswered(false);
        q.setMultiSelect(false);
        insert(q);
end

rule "Do not like boss"
    when
        Answer(questionId == "relationMale", value == "Co-worker")
        Answer(questionId == "isBoss", value == "Yes")
        Answer(questionId == "likeBoss", value == "No")
    then
        insert(new Recommendation("Holiday 4-Pack (weekday games)", "Get him a Holiday 4-Pack consisting of 4 weekday, day games and enjoy the extra time he is out of the office"));
end

rule "Like boss"
    when
        Answer(questionId == "relationMale", value == "Co-worker")
        Answer(questionId == "isBoss", value == "Yes")
        Answer(questionId == "likeBoss", value == "Yes")
    then
        insert(new Recommendation("Holiday 4-Pack", "Get him a Holiday 4-Pack. Can you say 'promotion'?"));
end
